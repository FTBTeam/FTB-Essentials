buildscript {
    repositories {
        jcenter()
        maven { url "https://maven.minecraftforge.net/" }
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.1.0'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'maven'

apply from: 'https://files.latmod.com/public/markdown-git-changelog.gradle'

def ENV = System.getenv()
version = ENV.BUILD_NUMBER ? "${mod_version}.${ENV.BUILD_NUMBER}" : "${mod_version}.custom"
group = project.mod_package
archivesBaseName = project.mod_name

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: project.mapping_version
    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run')
            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            if (project.hasProperty('mc_dev_uuid')) args '--uuid', project.mc_dev_uuid
            if (project.hasProperty('mc_dev_username')) args '--username', project.mc_dev_username
            if (project.hasProperty('mc_dev_width')) args '--width', project.mc_dev_width
            if (project.hasProperty('mc_dev_height')) args '--height', project.mc_dev_height

            mods {
                ftbessentials {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            args 'nogui'

            mods {
                ftbessentials {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            args '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/')

            mods {
                ftbessentials {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    // Temp
    maven { url = 'https://libraries.minecraft.net/' }
    mavenCentral()
    maven { url = 'https://maven.minecraftforge.net/' }

    mavenLocal()
    maven { url = "https://www.cursemaven.com" }
    maven { url "https://maven.saps.dev/minecraft" }
}

dependencies {
    minecraft "net.minecraftforge:forge:${forge_version}"
    compile fg.deobf("curse.maven:jei-238222:${jei_file}")
    compile(fg.deobf("dev.ftb.mods:ftb-ranks-forge:${ftbranks_version}"))
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : project.mod_id,
                "Specification-Vendor"    : project.mod_author,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : project.mod_author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}

if (ENV.LOCAL_MAVEN) {
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "file://${ENV.LOCAL_MAVEN}")
            }
        }
    }
}

if (ENV.CURSEFORGE_KEY) {
    curseforge {
        apiKey = ENV.CURSEFORGE_KEY
        project {
            id = project.curseforge_id
            releaseType = "release"
            addGameVersion "1.16.2"
            addGameVersion "1.16.3"
            addGameVersion "1.16.4"
            addGameVersion "1.16.5"
            relations {
                optionalDependency 'ftb-ranks'
            }
            changelog = getGitChangelog
            changelogType = 'markdown'
        }
    }
}